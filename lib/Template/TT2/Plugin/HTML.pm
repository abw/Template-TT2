package Template::TT2::Plugin::HTML;

use Template::TT2::Class
    version    => 0.01,
    debug      => 0,
    base       => 'Template::TT2::Plugin',
    constants  => 'HASH LAST';


sub new {
    my ($class, $context, @args) = @_;
    my $hash = @_ && ref $args[LAST] eq HASH ? pop @args : { };
    bless {
        _SORTED => $hash->{ sorted } || 0,
        _FILTER => $context->filter('html'),
    }, $class;
}


sub element {
    my ($self, $name, $attr) = @_;
    ($name, $attr) = %$name if ref $name eq HASH;
    return '' unless defined $name and length $name;
    $attr = $self->attributes($attr);
    $attr = " $attr" if $attr;
    return "<$name$attr>";
}


sub attributes {
    my ($self, $hash) = @_;

    return '' 
        unless ref $hash eq HASH 
            || is_object(HASH, $hash);

    my @keys = keys %$hash;
    @keys = sort @keys if $self->{ _SORTED };

    join(' ', map { 
        "$_=\"" . $self->escape( $hash->{ $_ } ) . '"';
    } @keys);
}


sub escape {
    my ($self, $text) = @_;
    $self->{ _FILTER }->($text);
}


sub url {
    my ($self, $text) = @_;
    return undef unless defined $text;
    $text =~ s/([^a-zA-Z0-9_.-])/uc sprintf("%%%02x",ord($1))/eg;
    return $text;
}


1;

__END__

=head1 NAME

Template::TT2::Plugin::HTML - Plugin to create HTML elements

=head1 SYNOPSIS

    [% USE HTML %]
    
    [% HTML.escape("if (a < b && c > d) ...") %]
    
    [% HTML.element(table => { border => 1, cellpadding => 2 }) %]
    
    [% HTML.attributes(border => 1, cellpadding => 2) %]

=head1 DESCRIPTION

This is a simple plugin implementing some useful methods for generating 
HTML.  

=head1 METHODS

=head2 escape(text)

Returns the source text with any HTML reserved characters such as 
C<E<lt>>, C<E<gt>>, etc., correctly esacped to their entity equivalents.

=head2 attributes(hash)

Returns the elements of the hash array passed by reference correctly
formatted (e.g. values quoted and correctly escaped) as attributes for
an HTML element.

=head2 element(type, attributes)

Generates an HTML element of the specified type and with the attributes
provided as an optional hash array reference as the second argument or
as named arguments.

    [% HTML.element(table => { border => 1, cellpadding => 2 }) %]
    [% HTML.element('table', border=1, cellpadding=2) %]
    [% HTML.element(table => attribs) %]

=head1 DEBUGGING

The HTML plugin accepts a C<sorted> option as a constructor argument
which, when set to any true value, causes the attributes generated by
the C<attributes()> method (either directly or via C<element()>) to be
returned in sorted order.  Order of attributes isn't important in
HTML, but this is provided mainly for the purposes of debugging where
it is useful to have attributes generated in a deterministic order
rather than whatever order the hash happened to feel like returning
the keys in.

    [% USE HTML(sorted=1) %]
    [% HTML.element( foo => { charlie => 1, bravo => 2, alpha => 3 } ) %]

generates:

    <foo alpha="3" bravo="2" charlie="1">

=head1 AUTHOR

Andy Wardley L<http://wardley.org/>

=head1 COPYRIGHT

Copyright (C) 1996-2008 Andy Wardley.  All Rights Reserved.

This module is free software; you can redistribute it and/or
modify it under the same terms as Perl itself.

=head1 SEE ALSO

L<Template::TT2::Plugin>

=cut

# Local Variables:
# mode: perl
# perl-indent-level: 4
# indent-tabs-mode: nil
# End:
#
# vim: expandtab shiftwidth=4:
